# Maintainer: Edgar <Edgar{at}AnotherFoxGuy.com>

pkgname=angelscript
pkgver=2.35.1
pkgrel=1
pkgdesc="An extremely flexible cross-platform scripting library"
arch=('i686' 'x86_64')
url="http://angelcode.com/angelscript/"
license=('custom:ZLIB')
source=("http://angelcode.com/angelscript/sdk/files/${pkgname}_${pkgver}.zip"
  LICENSE)
sha512sums=('b15083c7a77434c291e72ea82cfbab7734fa79df654d911a822f306d526669ebe9e55a981e8a1914deda0d2a52ebdc0ffb51a4179f307632c8c7d74b1abc69fa'
  '89225460b1f342d4611af27f5982d5d36c41b6948d9c72cb8d6a616b730eb830446a87e68e3a37ac2df41200d7637ec6338f935d8f88f9891d6b1b3fb4f10bb2')

build() {
  cd "$srcdir/sdk/angelscript/projects/cmake"

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd "$srcdir/sdk/angelscript/projects/cmake"

  make PREFIX="/usr" DESTDIR="$pkgdir" install

  msg "Installing add-ons..."
  cd "$srcdir/sdk"
  mkdir -p "$pkgdir/usr/share/$pkgname/"
  cp -rf add_on "$pkgdir/usr/share/$pkgname/"
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
