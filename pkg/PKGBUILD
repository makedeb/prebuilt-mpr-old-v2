# Maintainer: Hunter Wittenborn <hunter@hunterwittenborn.com>
pkgname=mari
pkgver=0.7.0
pkgrel=1
pkgdesc='The official helper for the makedeb Package Repository'
arch=('any')
depends=(
    'libapt-pkg-dev'
    'libssl-dev'
)
optdepends=(
    'r!less'
)
makedepends=(
    'asciidoctor'
    'cargo'
    'pkg-config'
)
license=('GPL3')
url='https://github.com/makedeb/mari'

# Transition stuff for the package rename from 'mist' to 'mari'.
conflicts=('mari<0.7.0')
replaces=("${conflicts[@]}")

source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('SKIP')

build() {
    cd "${pkgname}-${pkgver}/"
    cargo build --release

    sed -i "s|:mansource: Git|:mansource: ${pkgver}|" man/mari.1.adoc
}

package() {
    cd "${pkgname}-${pkgver}/"
    install -Dm 755 target/release/mari "${pkgdir}/usr/bin/mari"
    asciidoctor -b manpage -o - man/mari.1.adoc | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/man/man1/mari.1"
    install -Dm 644 completions/mari.bash "${pkgdir}/usr/share/bash-completion/completions/mari"

    # Transition stuff for the package rename from 'mist' to 'mari'.
    ln -s '/usr/bin/mari' "${pkgdir}/usr/bin/mist"
}

# vim: set sw=4 expandtab:
