# Maintainer: Matt Spaulding <matt@mattops.io>
#
# Improvement over Debian packaged version.
# Includes systemd service script and latest code version with CVE fixes.

pkgname=trousers
pkgver=0.3.15
pkgrel=1
pkgdesc="Open-source TCG Software Stack implementation for use with a TPM"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/trousers"
license=('CPL')
depends=('tpm-udev')
options=('libtool')
conflicts=('libtspi1' 'libtspi-dev')
provides=("libtspi1=$pkgver" "libtspi-dev=$pkgver")
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'sysusers.conf'
        'tmpfiles.conf'
        'tcsd.service'
        '01-tss-user.patch')
sha256sums=('1e5be93e518372acf1d92d2f567d01a46fdb0b730487e544e6fb896c59cac77f'
            '4c2684501e7a67132d4f53466b32fc06bd2a0275d4cf1fbd5479d0cdb2349bc7'
            '225414a8da825351da33760a141a607f731c513c36527e4624b36a607c5aa7e6'
            '653c3a94ec628e7a7fb150d0d289b0866c6b3bb9aa57af2c59724517d205d7bd'
            'd6d2f72389ffc1f61c363d839de24ba7ebb9a6453d99ece151a65933ebb0235c')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/01-tss-user.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./bootstrap.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --libdir=/usr/lib \
              --sbindir=/usr/bin \
              --with-openssl=/usr \
              --with-gui=openssl
  make
}

package_trousers() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -dm755 ${pkgdir}/usr/share/doc/${pkgname}
  install -m644 {ChangeLog,README,README.selinux} ${pkgdir}/usr/share/doc/${pkgname}/
  install -m644 LICENSE ${pkgdir}/usr/share/doc/${pkgname}/copyright
  install -Dm644 ${srcdir}/tcsd.service ${pkgdir}/usr/lib/systemd/system/tcsd.service
  install -Dm644 ${srcdir}/sysusers.conf ${pkgdir}/usr/lib/sysusers.d/trousers.conf
  install -Dm644 ${srcdir}/tmpfiles.conf ${pkgdir}/usr/lib/tmpfiles.d/trousers.conf
}

# vim:set ts=2 sw=2 et:
