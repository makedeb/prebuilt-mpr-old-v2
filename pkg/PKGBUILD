# Maintainer: only_vip <onlyme_vip@protonmail.com>
_pkgname=bspwm
pkgname=${_pkgname}-git
pkgver=11560df32022.02.15
pkgrel=2
pkgdesc='A tiling window manager based on binary space partitioning'
arch=('i686' 'amd64')
url="https://github.com/baskerville/${_pkgname}"
license=('custom:BSD')
depends=('bash' 'libxcb-util1' 'libxcb-ewmh2' 'libxcb-keysyms1')
makedepends=('git' 'libxcb-keysyms1-dev' 'libxcb-util-dev' 'libxcb-icccm4-dev' 'libxcb-ewmh-dev')
optdepends=('sxhkd: to define keyboard and pointer bindings'
	    #'xtitle-git: for the example panel'
	    #'sutils-git: for the example panel'
	    'font-terminus: for the example panel'
	    'python3: for some scripting examples')
source=("${pkgname}::git+https://github.com/baskerville/${_pkgname}.git")
md5sums=('SKIP')
provides=("bspwm")
conflicts=("bspwm")

pkgver() {
  cd ${pkgname}
    _always=$(git describe --always | sed -e 's:-:.:g') # tag is mobile, and switches between numbers and letters, can't use it for versioning
    _commits=$(git rev-list --count HEAD | sed 's:-:.:g') # total commits is the most sane way of getting incremental pkgver
    _date=$(git log -1 --date=short --pretty=format:%cd)
    printf "%s%s%s\n" "${_commits}" "${_always}" "${_date}" | sed -e 's:-:.:g'  -e 's:_:.:g'
}


build() {
  cd ${pkgname}
  make PREFIX=/usr
}

package() {
  cd ${pkgname}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
