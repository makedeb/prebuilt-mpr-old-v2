# Maintainer: canony <canony@protonmail.ch>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Florian Walch <florian+aur@fwalch.com>
# Contributor: Florian Hahn <flo@fhahn.com>

pkgname=neovim-bin
pkgver=0.7.0
pkgrel=1
pkgdesc='Fork of Vim aiming to improve user experience, plugins, and GUIs'
arch=('x86_64')
url='https://neovim.io'
license=('custom:neovim')
provides=('neovim' 'neovim-runtime')
depends=('libluajit-5.1-2' 'libluajit-5.1-common' 'libmsgpackc2' 'libtermkey1' 'libunibilium4' 'libvterm0' 'lua-luv')
optdepends=('python-neovim: for Python 3 plugin support (see :help python)'
            'xclip: for clipboard support on X11 (or xsel) (see :help clipboard)'
            'xsel: for clipboard support on X11 (or xclip) (see :help clipboard)'
            'wl-clipboard: for clipboard support on wayland (see :help clipboard)')
conflicts=('neovim' 'neovim-git' 'neovim-runtime')
source=(
        "https://github.com/neovim/neovim/releases/download/v${pkgver}/nvim-linux64.tar.gz"
        "ex"
        "view"
        "vimdiff")
postinst="${pkgname}.postinst"
prerm="${pkgname}.prerm"
sha256sums=('5b3fced3f185ae1e1497cb5f949597c4065585fc26e7cd25a31f5f791dbd9b59'
            '5d3178628afc39f0318638cad19e9ad06ada89926dfb263e93ab35cc8f60e911'
            '3cb963a264b6cf45749627408b5a62c6945e5e426b595bb5e6a388194c2c1990'
            '1d3123b9d97a4889696ab72db558041b5cca8e26ff51ae557f7842242a7d4ed1')

package() {
  echo "Copying application binary"
  install -Dm 755 nvim-linux64/bin/nvim -t "${pkgdir}/usr/bin/"

  echo "Copying libraries"
  install -Dm 644 nvim-linux64/lib/nvim/parser/c.so -t "${pkgdir}/usr/lib/nvim/parser/"

  echo "Copying application files"
  mkdir -p "${pkgdir}/usr/share/"
  cp -r nvim-linux64/share/applications "${pkgdir}/usr/share/applications"
  cp -r nvim-linux64/share/icons "${pkgdir}/usr/share/icons"
  cp -r nvim-linux64/share/locale "${pkgdir}/usr/share/locale"
  cp -r nvim-linux64/share/man "${pkgdir}/usr/share/man"
  cp -r nvim-linux64/share/nvim "${pkgdir}/usr/share/nvim"

  echo "Copying scripts for update-alternatives symbolic links"
  mkdir -p "${pkgdir}/usr/libexec/neovim"
  install -Dm 755 ex "${pkgdir}/usr/libexec/neovim/"
  install -Dm 755 view "${pkgdir}/usr/libexec/neovim/"
  install -Dm 755 vimdiff "${pkgdir}/usr/libexec/neovim/"
}

# vim:set sw=2 sts=2 et:
