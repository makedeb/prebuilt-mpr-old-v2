# Maintainer: Hunter Wittenborn <hunter@hunterwittenborn.com>
pkgname=mpr-cli
pkgver=0.3.2
pkgrel=1
pkgdesc='The official command-line interface for the makedeb Package Repository'
arch=('any')
depends=(
    'libssl-dev'
)
optdepends=(
    'r!less'
)
makedepends=(
    'asciidoctor'
    'cargo'
    'pkg-config'
)
license=('GPL3')
url='https://github.com/makedeb/mpr-cli'

source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('SKIP')

build() {
    cd "${pkgname}-${pkgver}/"
    cargo build --release

    sed -i "s|:mansource: Git|:mansource: ${pkgver}|" man/mpr.1.adoc
}

package() {
    cd "${pkgname}-${pkgver}/"
    install -Dm 755 target/release/mpr "${pkgdir}/usr/bin/mpr"
    asciidoctor -b manpage -o - man/mpr.1.adoc | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/man/man1/mpr.1"
    install -Dm 644 completions/mpr.bash "${pkgdir}/usr/share/bash-completion/completions/mpr"
}

# vim: set sw=4 expandtab:
