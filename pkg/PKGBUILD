# Maintainer: Hunter Wittenborn <hunter@hunterwittenborn.com>

# NOTE: If you're installing this from the MPR you'll need to pass
# `-H 'MPR-Package: yes'` to your `makedeb` call if you want Mist to be able to
# automatically update itself.
pkgname=mist
pkgver=0.9.5
pkgrel=1
pkgdesc='The official command-line interface for the makedeb Package Repository'
arch=('any')
depends=(
    'libapt-pkg-dev'
    'libssl-dev'
    'sudo'
)
optdepends=(
    'r!less'
)
makedepends=(
    'asciidoctor'
    'rustup'
    'pkg-config'
)
license=('GPL3')
url='https://github.com/makedeb/mist'
postinst='mist.postinst'

source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('SKIP')

build() {
    cd "${pkgname}-${pkgver}/"
    cargo build --release
    sed -i "s|:mansource: Git|:mansource: ${pkgver}|" man/mist.1.adoc
}

package() {
    cd "${pkgname}-${pkgver}/"
    install -Dm 755 target/release/mist "${pkgdir}/usr/bin/mist"
    asciidoctor -b manpage -o - man/mist.1.adoc | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/man/man1/mist.1"
    install -Dm 644 completions/mist.bash "${pkgdir}/usr/share/bash-completion/completions/mist"
}

# vim: set sw=4 expandtab:
