# Maintainer: hiddeninthesand <hiddeninthesand at pm dot me>
# Contributor:         steiner <daimarstein@pm.me>

## AUR Maintainer:     barfin <barfin@protonmail.com>
## AUR Co-Maintainer:  Jaja <jaja@mailbox.org>
## AUR Co-Maintainer:  floriplum <floriplum@mailbox.org>

## pkginfo
pkgdesc='A fancy custom distribution of Valves Proton with various patches'
pkgname='proton-ge-custom-bin'
pkgver='7.31'
pkgrel='1'
arch=('x86_64')
license=('BSD' 'LGPL' 'zlib' 'MIT' 'MPL' 'custom')
changelog='changelog.md'
provides=('proton' "proton-ge-custom=${pkgver/_/.}")
conflicts=('proton-ge-custom')

## dependencies
makedepends=('patch')
depends=('python3' 'libvulkan1' 'flac' 'speex' 'libgstreamer1.0-0' 'libgudev-1.0-0' 'mpg123' 'libtheora0' 'ffmpeg' 'libopenal1' 'va-driver-all' 'libvdpau1' 'libturbojpeg')
optdepends=('kdialog: KDE splash dialog support'
	'zenity: GNOME splash dialog support'
	'steam: use proton with steam like intended'
	'vulkan-driver: driver to be used by DXVK'
	'winetricks: protonfixes backend - highly recommended'
	'wine: support for 32bit prefixes'
	'xboxdrv: gamepad driver service')
## makepkg options
options=(!strip emptydirs)

## fix naming conventions, matching upstream
_pkgname="${pkgname//-bin/}"
_srcdir="GE-Proton${pkgver//./-}"

## paths and files
_protondir="usr/share/steam/compatibilitytools.d/${_pkgname}"
_licensedir="usr/share/licenses/${_pkgname}"
_execfile='usr/bin/proton'
_protoncfg="${_protondir}/user_settings.py"

## user edited files to backup
backup=("/${_protoncfg}")

## sources
url='https://github.com/GloriousEggroll/proton-ge-custom'
source=("${_pkgname}-${pkgver}.tar.gz::${url}/releases/download/${_srcdir}/${_srcdir}.tar.gz"
	'user_settings.py'
	'launcher.sh')
b2sums=('72205d073e3e844a7ab6a9f778736150dc0aeaa904ffa7ec0188cfd7c73318abbdf9f9d4f059530bca4bbe5b446ca81ad21bf7c7b803bea7497b7dfa8b79d6b2'
        'c0f466bc55ea4aadda12800d1a2b55fe2bae75a6f547f8275115913f5c6da1728a64a5269d938e315fba9518dd0b7e1ac41ecf5fc3109b316399a1ecfbb87492'
        'f2df30593cc5a72959dc14236e5958006f9c64d51fcea48de38df19da1428446fe6a2f0bd080736f60a4b40816f929559667d49c888b4a3793af88c9c12a6df4')

build() {
	## patches
	sed -i "s|_proton=echo|_proton=/${_protondir}/proton|" "${srcdir}"/launcher.sh
	sed -i -r 's|"GE-Proton.*"|"Proton-GE"|' "${_srcdir}"/compatibilitytool.vdf
	## remove artifacts
	rm "${_srcdir}"/protonfixes/*.tar.xz
	rm -rf "${_srcdir}"/protonfixes/.git*
	## fixes from namcap inspection
	strip --preserve-dates --strip-unneeded "${_srcdir}"/files/bin/wine*
}

package() {
	## create paths
	install -d "${pkgdir}/${_protondir}/"
	install -d "${pkgdir}/${_licensedir}/"
	install -d "${pkgdir}/$(dirname ${_execfile})/"
	## licenses
	mv "${_srcdir}/LICENSE" "${pkgdir}/${_licensedir}/license"
	mv "${_srcdir}/LICENSE.OFL" "${pkgdir}/${_licensedir}/license_OFL"
	mv "${_srcdir}/PATENTS.AV1" "${pkgdir}/${_licensedir}/license_AV1"
	mv "${_srcdir}/protonfixes/LICENSE" "${pkgdir}/${_licensedir}/license_protonfixes"
	## config files
	install --mode=0775 --group=50 "${srcdir}"/user_settings.py "${pkgdir}/${_protoncfg}"
	## executables
	mv "${_srcdir}"/* "${pkgdir}/${_protondir}"
	install --mode=0755 "${srcdir}"/launcher.sh "${pkgdir}/${_execfile}"
}
