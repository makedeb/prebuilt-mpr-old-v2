# vim: ft=sh syntax=sh softtabstop=2 tabstop=2 shiftwidth=2 fenc=utf-8 expandtab
# Maintainer: alfador <the.alfador@pm.me>
_pkgname="delta"
pkgname="${_pkgname}-bin"
pkgver="0.8.3"
pkgrel=1
pkgdesc="A syntax-highlighting pager for git"
arch=("x86_64" "aarch64")
provides=("delta")
conflicts=("delta")
url="https://github.com/dandavison/${_pkgname}"
license=("MIT")
source_x86_64=("${pkgname}-${CARCH}.tar.gz::https://github.com/dandavison/${_pkgname}/releases/download/${pkgver}/${_pkgname}-${pkgver}-${CARCH}-unknown-linux-gnu.tar.gz")
source_aarch64=("${pkgname}-${CARCH}.tar.gz::https://github.com/dandavison/${_pkgname}/releases/download/${pkgver}/${_pkgname}-${pkgver}-${CARCH}-unknown-linux-gnu.tar.gz")
sha512sums_x86_64=("fda389c3bb9168dde81c9c679311bfe79e4ed05a1c5dc0813a022898e503875fa23e18a493d831f5a35b9409ff02716f9e4794544786d7498682ec4bc2fabc0c")
sha512sums_aarch64=("11a46cb393b678cfbf35bc62b598e87b40d654e6a5d6e9bfd3387b0a51d887ed0d17628f79bf2025c94384ee45b0ff98e13a00f47d6e02c3e72630ff8f70ce14")
check() {
  [ "$("${srcdir}/${_pkgname}-${pkgver}-${CARCH}-unknown-linux-gnu/delta" --version)" != "${_pkgname} ${pkgver}" ] && return 1 || return 0
}
package() {
  install -Dm755 "${srcdir}/${_pkgname}-${pkgver}-${CARCH}-unknown-linux-gnu/delta" "${pkgdir}/usr/bin/delta"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}-${CARCH}-unknown-linux-gnu/LICE"* "${pkgdir}/usr/share/doc/${pkgname}/copyright"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}-${CARCH}-unknown-linux-gnu/README.md"* "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
